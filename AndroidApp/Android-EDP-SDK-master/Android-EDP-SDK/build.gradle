/*
 * Copyright (c) 2016-2018. China Mobile Communications Corporation. All rights reserved. 
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion rootProject.targetSdkVersion
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}

def siteUrl = 'https://github.com/cm-heclouds/Android-EDP-SDK'
def gitUrl = 'https://github.com/cm-heclouds/Android-EDP-SDK.git'
group = "com.chinamobile.iot.onenet.edp"
version = "1.1.1"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name ''
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://mit-license.org/'
                    }
                }
                developers {
                    developer {
                        id 'chenglei'
                        name 'chenglei'
                        email 'chenglei.1986@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8"
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "Android-EDP-SDK"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}